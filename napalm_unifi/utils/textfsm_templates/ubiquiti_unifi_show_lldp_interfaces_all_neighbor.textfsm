Value NEIGHBOR_CHASSIS_ID ([a-f0-9]{6}-[a-f0-9]{6}|(?:[a-z0-9]{2}\s){5}[a-z0-9]{2}|\S+)
Value NEIGHBOR_PORTID (.+?)
Value NEIGHBOR_SYSNAME (.+?)
Value SYSTEM_DESCR (.+?)
Value PORT_DESCR (.*?)
Value SYSTEM_CAPABILITIES_ENABLED (.*?)

Start
  ^\s*$$ -> Block
  ^.*$$ -> Error

Block
  ^\s*Device\s+ID:\s+${NEIGHBOR_CHASSIS_ID}\s*$$
  ^\s*Port\s+ID:\s+${NEIGHBOR_PORTID}\s*$$
  ^\s*System\s+Name:\s+${NEIGHBOR_SYSNAME}\s*$$
  ^\s*Capabilities:\s+${SYSTEM_CAPABILITIES_ENABLED}\s*$$
  ^\s*System\s+description:\s+${SYSTEM_DESCR}\s*$$
  ^\s*Port\s+description:\s+${PORT_DESCR}\s*$$
  ^\s*Time\s+To\s+Live:\s+\d+.*$$
  ^\s*802.3\s+MAC/PHY\s+Configur/Status\s*$$
  ^\s*Auto-negotiation support:\s+
  ^\s*Auto-negotiation status:\s+
  ^\s*Auto-negotiation Advertised Capabilities:\s+
  ^\s*Operational MAU type:\s+
  ^\s*LLDP-MED capabilities:\s+.+$$
  ^\s*LLDP-MED Device type:\s+.+$$
  ^\s*LLDP-MED\s+Network\s+policy -> MedNetworkPolicy
  ^\s*LLDP-MED\s+Power\s+over\s+Ethernet -> MedPowerOverEthernet
  ^\s*Management\s+address:\s+
  ^\s*Hardware\s+revision:
  ^\s*Firmware\s+revision:
  ^\s*Software\s+revision:
  ^\s*Serial\s+number:
  ^\s*Manufacturer\s+Name:
  ^\s*Model\s+name:
  ^\s*Asset\s+ID:
  ^\s*$$ -> Record EOF
  ^.*$$ -> Error


MedNetworkPolicy
  ^\s*Application\s+type:\s+.*$$
  ^\s*Flags:\s+\S+\s*$$
  ^\s*VLAN\s+ID:\s+\d+\s*$$
  ^\s*Layer\s+2\s+priority:\s+\d+\s*$$
  ^\s*DSCP:\s+\d+\s*$$ -> Block

MedPowerOverEthernet
  ^\s*Device\s+Type:\s+.+$$
  ^\s*Power\s+Source:\s+.+$$
  ^\s*Power\s+priority:\s+.+$$
  ^\s*Power\s+value:\s+.+$$ -> Block
